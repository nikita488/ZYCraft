buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = "${minecraft_version}-${mod_version}"
group = 'nikita488.zycraft'
archivesBaseName = 'ZYCraft'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

sourceSets.main {
    ext.refMap = "mixins.zycraft.refmap.json"
}

repositories {
    mavenLocal()
    maven { url "https://maven.tterrag.com/" }
    maven { url = "https://www.cursemaven.com" }
}

minecraft {
    mappings channel: 'official', version: "${minecraft_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            //property 'forge.logging.console.level', 'debug'
            arg '-mixin.config=mixins.zycraft.json'

            mods {
                zycraft {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            //property 'forge.logging.console.level', 'debug'

            mods {
                zycraft {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            //property 'forge.logging.console.level', 'debug'
            args '--mod', 'zycraft', '--all', '--output', file('src/generated/resources/'), '--existing', sourceSets.main.resources.srcDirs[0]

            mods {
                zycraft {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    shade
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    def registrate = "com.tterrag.registrate:Registrate:MC1.16.2-${registrate_version}"
    implementation fg.deobf(registrate)
    shade registrate

    implementation fg.deobf("team.chisel.ctm:CTM:MC1.16.1-${ctm_version}")

    //runtimeOnly fg.deobf("curse.maven:mekanism-268560:3183270")
    runtimeOnly fg.deobf("curse.maven:jei-238222:3157864")

    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
}

processResources {
    // Exclude datagenerator .cache directory
    exclude '.cache'
}

jar {
    classifier = 'slim'

    manifest {
        attributes([
                "Specification-Title": "zycraft",
                "Specification-Vendor": "nikita488",
                "Specification-Version": "1.0",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"nikita488",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConnector": "nikita488.zycraft.mixin.ZYMixinConnector"
        ])
    }
}

task replaceResources(type: Copy) {
    outputs.upToDateWhen { false }
    from(sourceSets.main.resources) {
        include "META-INF/mods.toml"
        expand "version": mod_version, "mc_version": minecraft_version, "forge_version": min_forge_version, "loader_version": loader_version
    }
    into "$buildDir/resources/main/"
}

processResources {
    //Exclude the mods.toml file as we manually handle that and don't want it to invalidate our cache
    exclude 'META-INF/mods.toml'
    finalizedBy replaceResources
}

shadowJar {
    classifier = ''
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', 'nikita488.zycraft.repack.registrate'
}

reobf {
    shadowJar {}
}

task apiJar(type: Jar) {
    classifier = 'api'
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'nikita488/zycraft/api/**'
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

tasks.curseforge.enabled = project.hasProperty('api_key')

curseforge {
    if (project.hasProperty('api_key')) {
        apiKey = project.api_key
    }

    project {
        id = "${project_id}"
        changelog = file('CHANGELOG.md')
        changelogType = 'markdown'
        releaseType = project.hasProperty('release_type') ? project.release_type : 'beta'
        mainArtifact(shadowJar) {
            displayName = "ZYCraft ${version}"
        }
        addArtifact apiJar
        relations {
            requiredDependency 'ctm'
        }
    }
}
